#require 6.5100

#usage "en: ULP - Library"

string Version="0.1";
string Name="<b>SMD tCream</b>";


string abc,temp;
string lst_packages[];
int Selected = -1, line_Selected = -1;

string Headers = "Name\tX\tY\tdx\tdy\tAngle";
string t_line;
string t_data[];

string script_filename = "add_tCream.scr";
string script_path;

if (library) {
  library(L) {
    script_path = filedir(L.name);
  }
}

// === functions ===
void get_packages(void){
	int cnt=0;
	library(L) {
	L.packages(PAC) {
		lst_packages[cnt]=PAC.name;cnt++;
	}
	}
}



void runscript(void) {
	int Result=0;
	string script;
	int s = fileread(script, script_path + script_filename);
	Result = dlgDialog("Accept Script?") {
		dlgHBoxLayout dlgSpacing(300);
		dlgHBoxLayout {
			dlgVBoxLayout dlgSpacing(300);
			dlgTextEdit(script);
		}
		dlgHBoxLayout {
			dlgStretch(0);
			dlgPushButton("+Run script") dlgAccept();
			dlgStretch(1);
			dlgPushButton("-Cancel") dlgReject();
			dlgStretch(0);
		}
	};
	
	if (Result == 1) exit ("script '" + script_path + script_filename + "'");
	else exit (0);
}

void calc_tc_box(string data_line){
	real h_min=0.1, w_min=0.1;//mm
	real sz_perc=0.8;//80%
	
	string list[];
	strsplit(list, data_line, '\t');
	
	real x, y, dx, dy;
	x = strtod(list[1]);
	y = strtod(list[2]);
	dx = strtod(list[3]);
	dy = strtod(list[4]);
	
	real h, w, x1, x2, y1, y2;
	//calc h,w of the box
	h = sz_perc*dy;
	w = sz_perc*dx;
	
	//constraint box size
	if (h<h_min) h=h_min;
	if (w<w_min) w=w_min;
	
	x1 = x - (w/2);
	x2 = x + (w/2);
	
	y1 = y - (h/2);
	y2 = y + (h/2);
	
	output(script_path + script_filename, "wtD"){
		printf("change layer 31;\n");
		printf("RECT (%f %f)(%f %f);\n", x1, y1, x2, y2);
	}
	
	runscript();
}

//Get information (e.g. position, size, angle, ...) about a package
//and show them in a table
void get_smd_info(string pac_name){
	int data_count=0;
	
	////====================
	library(L) {
	L.packages(PAC) {
		if(PAC.name==pac_name){
			PAC.contacts(C) {
				if (C.smd && C.smd.layer==1){
					sprintf(t_line,"%s\t%.2f\t%.2f\t%.2f\t%.2f\t%.1f",
						C.name, u2mm(C.x), u2mm(C.y), u2mm(C.smd.dx), u2mm(C.smd.dy), C.smd.angle);
					t_data[data_count]=t_line;
					data_count++;
				}
			}
		}
	}		
	}
	dlgDialog("Window 2") {
		dlgHBoxLayout dlgSpacing(300);
		dlgLabel("<b>"+pac_name+"</b>");
		dlgListView(Headers, t_data, line_Selected) calc_tc_box(t_data[line_Selected]);
		string num_smds;
		sprintf(num_smds,"%d",data_count);
		dlgLabel("# SMDs: " + num_smds);
	};
}

//main
if (library) {
	int Result = 0;
	Result = dlgDialog("Select Package") {
		//GUI
		dlgHBoxLayout dlgSpacing(200);
		dlgLabel(Name);
		get_packages();
		
		dlgComboBox(lst_packages, Selected) get_smd_info(lst_packages[Selected]); 
		
		dlgLabel("Version " + Version);
	};

}
else{
	
	dlgMessageBox("! Open in Library.","+OK");
	exit(-1);
}